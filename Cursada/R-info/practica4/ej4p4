{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso juntarFlores
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
  fin
  proceso juntarPapeles
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
  fin
  proceso vaciarBolsa
  comenzar
    mientras(HayFlorEnLaBolsa)
      depositarFlor
    mientras(HayPapelEnLaBolsa)
      depositarPapel    
  fin
  proceso inicializar
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
  fin
  proceso finalizar
  comenzar
    EnviarMensaje(4,r1)
    EnviarMensaje(4,r2)
  fin
areas
  tareas: AreaPC (2,2,100,100)
  areaJ: AreaP (1,1,1,1)
  area1: AreaP (2,1,2,1)
  area2: AreaP (3,1,3,1)
robots
  robot trabajador
  variables
    id, tarea, av, ca, ai, ci: numero
  comenzar
    RecibirMensaje(id,rj)
    ai:= PosAv
    ci:= PosCa
    RecibirMensaje(tarea,rj)
    mientras(tarea <> 4)
      RecibirMensaje(av, rj)
      RecibirMensaje(ca, rj)
      BloquearEsquina(av,ca)
      Pos(av,ca)
      si(tarea = 1)
        juntarFlores
      sino
        si (tarea = 2)
          juntarPapeles
        sino
          si (tarea = 3)
            vaciarBolsa
      Pos(ai,ci)
      LiberarEsquina(av,ca)
      RecibirMensaje(tarea,rj)
  fin
  robot jefe
  variables
    quien, tarea, a, c: numero
  comenzar
    inicializar
    repetir 10 
      Random(quien,1,2)
      Random(tarea,1,3)
      Random(a,2,100)
      Random(c,2,100)
      si (quien = 1)
        EnviarMensaje(tarea, r1)
        EnviarMensaje(a,r1)        
        EnviarMensaje(c,r1)
      sino
        si (quien = 2)
          EnviarMensaje(tarea, r2)
          EnviarMensaje(a,r2)        
          EnviarMensaje(c,r2)
    finalizar
  fin
variables
  rj: jefe
  r1, r2: trabajador
comenzar
  AsignarArea(rj, areaJ)
  AsignarArea(r1, area1)
  AsignarArea(r1, tareas)
  AsignarArea(r2, area2)
  AsignarArea(r2, tareas)
  Iniciar(rj, 1,1)
  Iniciar(r1, 2,1)
  Iniciar(r2, 3,1)
fin